PROG    -> MAIN { CLASSE }

MAIN    -> class id '{' public static void main ( String [ ] id )'{' CMD '}' '}'

CLASSE  -> class id [ extends id ]'{' { VAR } { METODO }'}'

VAR     -> TIPO id ;

METODO  -> public TIPO id '(' [ PARAMS ]')' '{' { VAR } { CMD } return EXP ; '}'

PARAMS  -> TIPO id { , TIPO id }

TIPO    -> int INT1
        | boolean
        | id

INT1    -> '[' ']'
        | λ

CMD     -> '{' { CMD }'}'
        | if '(' EXP ')' CMD ELSE1
        | while '(' EXP ')' CMD
        | System.out.println '(' EXP ')' ;
        | id ID1

ELSE1   -> else CMD
        | λ

ID1     -> = EXP ;
        | '[' EXP ']' = EXP ;

EXP     -> EXP && REXP
        | REXP

REXP    -> REXP < AEXP
        | REXP == AEXP
        | REXP != AEXP
        | AEXP

AEXP    -> AEXP + MEXP
        | AEXP - MEXP
        | MEXP

MEXP    -> MEXP * SEXP
        | MEXP / SEXP
        | SEXP

SEXP    -> ! SEXP
        | - SEXP
        | true
        | false
        | num
        | new int '[' EXP ']'
        | PEXP . length
        | PEXP '[' EXP ']'
        | PEXP

PEXP    -> id
        | this
        | new id '(' ')'
        | '(' EXP ')'
        | PEXP . id
        | PEXP . id '(' [ EXPS ]')'
        
EXPS    -> EXP { , EXP }